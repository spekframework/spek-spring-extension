buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M3'
    }
}

plugins {
    id "com.zoltu.git-versioning" version "3.0.2"
    id "io.spring.dependency-management" version "1.0.0.RC2"
}

ZoltuGitVersioning {
    customDescribeProcessor { describeResults ->
        def matcher = (describeResults =~ /v(?<major>[0-9]+?)\.(?<minor>[0-9]+?)\.(?<patch>[0-9]+?)(?:\-(?<tags>[0-9A-Za-z\.\-]+))?\-(?<commitCount>[0-9]+?)\-g(?<sha>[a-zA-Z0-9]+)/)
        matcher.matches()
        [
            major: matcher.group('major'),
            minor: matcher.group('minor'),
            commitCount: matcher.group('commitCount'),
            sha: matcher.group('sha'),
            tags: matcher.group('tags'),
            patch: matcher.group('patch')
        ]
    }

    customVersionToString { versionInfo ->
        if (versionInfo.tags != null) {
            "${versionInfo.major}.${versionInfo.minor}.${versionInfo.patch}-${versionInfo.tags}"
        } else {
            "${versionInfo.major}.${versionInfo.minor}.${versionInfo.patch}"
        }
    }
}

afterEvaluate {
    println "##teamcity[buildNumber \'${version}\']"
    def v = version
    allprojects {
        version = v
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'

repositories {
    jcenter()
}

dependencyManagement {
    dependencies {
        dependencySet(group: 'org.springframework', version: '4.3.5.RELEASE') {
            entry 'spring-core'
            entry 'spring-context'
            entry 'spring-test'
        }

        dependencySet(group: 'org.spekframework.spek2', version: '2.0.5') {
            entry 'spek-dsl-jvm'
            entry 'spek-runner-junit5'
        }

        dependencySet(group: 'org.jetbrains.kotlin', version: kotlinVersion) {
            entry 'kotlin-stdlib-jdk8'
            entry 'kotlin-reflect'
            entry 'kotlin-test'
        }
    }
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compile 'org.jetbrains.kotlin:kotlin-reflect'
    compile 'org.jetbrains.kotlin:kotlin-test'
    compile 'org.springframework:spring-context'
    compile 'org.springframework:spring-test'
    compile "org.spekframework.spek2:spek-dsl-jvm"
    testRuntime "org.spekframework.spek2:spek-runner-junit5"
}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
}
